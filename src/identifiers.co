// Layout of the trie nodes

// Node next index size
comptime trie_node_next_size = 4;

comptime max_nexts = 0x80;

// Node value offset
comptime trie_node_value = max_nexts * trie_node_next_size;

// Size of an entire trie node, including next pointers, attribute and value
comptime trie_node_size = trie_node_value + (4 + 4 + 8);

zeroes addr_value_trie_bytes[8];
zeroes last_addr_value_trie[8];

import "src/syscalls.co" syscalls;

fn init() [num_bytes] {
    num_bytes = 0x10000 * trie_node_size;
    addr_value_trie_bytes[0] = syscalls.anon_mmap(num_bytes);
}

fn alloc() {
    last_addr_value_trie[0] += 1;
    return last_addr_value_trie[0];
}

fn node_addr(node_idx) {
    return (trie_node_size * node_idx) + addr_value_trie_bytes[0];
}

fn trie_node_next(node_ptr, idx) [tmp] {
    @assert(idx < max_nexts);
    tmp = node_ptr + (idx * trie_node_next_size);
    return @read32(tmp);
}

fn trie_node_set_next(node_ptr, idx, next) [tmp] {
    @assert(idx < max_nexts);
    tmp = node_ptr + (idx * trie_node_next_size);
    @write32(tmp, next);
}

fn lookup(name, idx) [addr, chr] {
    loop {
        chr = @read8(name);
        if(chr) {
            addr = node_addr(idx);
            idx = trie_node_next(addr, chr);
            name += 1;
            if(idx) {
                continue;
            } else {
                idx = alloc();
                trie_node_set_next(addr, chr, idx);
                continue;
            }
        } else {
            return idx;
        }
    }
}

comptime type_offset = trie_node_value;

fn node_get_type(node) {
    node = node_addr(node);
    node += type_offset;
    return @read32(node);
}

fn node_set_type(node, value) {
    node = node_addr(node);
    node += type_offset;
    return @write32(node, value);
}

comptime attribute_offset = type_offset + 4;

fn node_get_attribute(node) {
    node = node_addr(node);
    node += attribute_offset;
    return @read32(node);
}

fn node_set_attribute(node, value) {
    node = node_addr(node);
    node += attribute_offset;
    return @write32(node, value);
}

comptime value_offset = attribute_offset + 4;

fn node_get_value(node) {
    node = node_addr(node);
    node += value_offset;
    return @read64(node);
}

fn node_set_value(node, value) {
    node = node_addr(node);
    node += value_offset;
    return @write64(node, value);
}
