import "src/builtins.co" builtins;
import "src/codegen.co" codegen;
import "src/identifier_types.co" itypes;
import "src/identifiers.co" idents;
import "src/printer.co" printer;
import "src/source.co" source;
import "src/syscalls.co" syscalls;
import "src/tokenizer.co" tokenizer;
import "src/writer.co" writer;

zeroes import_file_root[8];
zeroes parse_top_level_ptr[8];
zeroes root_root[8];

fn root_ident(ident) [node] {
    node = root_root[0];
    return idents.lookup(ident, node);
}

fn assert_fail_addr() [result] {
    result = root_ident("assert_fail");
    @assert(idents.node_get_type(result) == itypes.function_addr);
    @assert(idents.node_get_attribute(result) == 0); // Shouldn't return
    return idents.node_get_value(result);
}

fn main_addr() [result] {
    result = root_ident("main");
    @assert(idents.node_get_type(result) == itypes.function_addr);
    @assert(idents.node_get_attribute(result) == 0); // Shouldn't return
    return idents.node_get_value(result);
}

fn parse_comptime_decl() [value_node, value] {
    codegen.discard();

    value_node = codegen.expect_token(tokenizer.identifier, "Expected identifier after `comptime`");
    codegen.discard();
    @assert(idents.node_get_type(value_node) == itypes.none);

    codegen.expect_token(tokenizer.assignment, "Expected `=` after identifier");
    codegen.discard();

    value = codegen.eval_comptime_expr(codegen.any_unambigous_expr);
    idents.node_set_value(value_node, value);
    idents.node_set_type(value_node, itypes.comptime_int);

    codegen.expect_token(tokenizer.semicolon, "Expected `;` after comptime declaration");
    codegen.discard();
}

fn parse_enum_decl() [next_value, node] {
    codegen.discard();

    codegen.expect_token(tokenizer.open_curly_brace, "Expected `{` after `enum`");
    codegen.discard();

    next_value = 0;

    loop {
        if(codegen.peek_type() == tokenizer.closing_curly_brace) {
            codegen.discard();
            codegen.expect_token(tokenizer.semicolon, "Expected `;` after enum declaration");
            codegen.discard();
            return;
        } else {
            node = codegen.expect_token(tokenizer.identifier, "Expected identifier or `}`");
            codegen.discard();

            @assert(idents.node_get_type(node) == itypes.none);

            if(codegen.peek_type() != tokenizer.comma) {
                codegen.expect_token(tokenizer.assignment, "Expected `=` or `,` after enum member identifier");
                codegen.discard();
                next_value = codegen.eval_comptime_expr(codegen.any_unambigous_expr);
            } else {}
            idents.node_set_type(node, itypes.comptime_int);
            idents.node_set_value(node, next_value);
            next_value += 1;

            codegen.expect_token(tokenizer.comma, "Expected `,` after enum member value");
            codegen.discard();
            continue;
        }
    }
}

fn set_source_file(file_path) [fd] {
    fd = syscalls.open(file_path, syscalls.O_RDONLY);
    source.switch_file(fd);
}

fn parse_additional_file(file_path, new_node) [fd, root_stash, file_context_stash[source.file_context_size], new_file_root, len] {
    // Store our current context, the imported file has no idea!
    root_stash = codegen.current_file_root[0];
    source.stash_file_info(file_context_stash);

    len = strings.len(file_path) + 1;
    @memcpy(source.file_name, file_path, len);

    set_source_file(file_path);

    @call(parse_top_level_ptr[0]);

    new_file_root = codegen.current_file_root[0];

    // Return to monke
    source.restore_file_info(file_context_stash);
    codegen.current_file_root[0] = root_stash;

    return new_file_root;
}

fn parse_file_if_needed(file_path) [node, new_file_root] {
    node = import_file_root[0];
    node = idents.lookup(file_path, node);

    switch(idents.node_get_type(node)) {
        printer.print_string("Bad filename ident type!\n");
        printer.exit(1);

    case itypes.none: // New file, parse first time
        idents.node_set_type(node, itypes.partially_parsed_filename);

        new_file_root = parse_additional_file(file_path, node);

        idents.node_set_type(node, itypes.fully_parsed_filename);
        idents.node_set_attribute(node, new_file_root);
        return new_file_root;

    case itypes.fully_parsed_filename: // Already parsed
        return idents.node_get_attribute(node);        

    case itypes.partially_parsed_filename:
        printer.print_string("Circular import detected!\n");
        printer.exit(1);
    }
}

fn parse_import() [file_root, ident_node] {
    codegen.discard();

    codegen.expect_token(tokenizer.string_literal, "Expected a string literal after `import`.");
    codegen.discard();
    file_root = parse_file_if_needed(tokenizer.buffer);

    codegen.expect_token(tokenizer.identifier, "Expected an identifier after filename.");
    ident_node = codegen.peek_value();
    codegen.discard();

    // Assert the identifier is unused
    @assert(idents.node_get_type(ident_node) == itypes.none);

    idents.node_set_type(ident_node, itypes.variable_scope);
    idents.node_set_attribute(ident_node, file_root);

    codegen.expect_token(tokenizer.semicolon, "Expected `;` after import");
    codegen.discard();
}

fn add_builtins_to_current_file() [node, builtin_root] {
    node = codegen.current_file_root[0];
    builtin_root = builtins.builtin_root[0];
    node = idents.node_addr(node);
    idents.trie_node_set_next(node, '@', builtin_root);
}

// Parse function argument and local variable list, creating identifiers
// and storing the argument values in their stack slots
fn read_stack_frame() [offset, num_args, local_var_space, ident, size] {
    offset = 0;
    num_args = 0;
    local_var_space = 0;

    if(codegen.peek_type() == tokenizer.open_paren) {
        codegen.discard();
        // Parse argument list

        loop {
            if(codegen.peek_type() == tokenizer.closing_paren) {
                codegen.discard();
                break;
            } else {
                ident = codegen.expect_token(tokenizer.identifier, "Expected identifier or `)`");
                codegen.discard();
                @assert(idents.node_get_type(ident) == itypes.none);

                idents.node_set_type(ident, itypes.local_variable);

                offset += 8;

                idents.node_set_value(ident, offset);

                num_args += 1;
            }

            if(codegen.peek_type() == tokenizer.comma) {
                codegen.discard();
            } else {}
            continue; // @BUG: I need this for some reason.
        }
    } else {}

    if(codegen.peek_type() == tokenizer.open_square_bracket) {
        codegen.discard();
        // Parse local var list
        loop {
            if(codegen.peek_type() == tokenizer.closing_square_bracket) {
                codegen.discard();
                break;
            } else {
                ident = codegen.expect_token(tokenizer.identifier, "Expected identifier or `]`");
                codegen.discard();
                @assert(idents.node_get_type(ident) == itypes.none);

                if(codegen.peek_type() == tokenizer.open_square_bracket) {
                    codegen.discard();

                    idents.node_set_type(ident, itypes.local_buffer);
                    size = codegen.eval_comptime_expr(codegen.any_unambigous_expr);

                    local_var_space += size;
                    offset += size;
                    idents.node_set_value(ident, offset);
                    idents.node_set_attribute(ident, size);

                    codegen.expect_token(tokenizer.closing_square_bracket, "Expected `]` after local buffer size");
                    codegen.discard();
                } else {
                    idents.node_set_type(ident, itypes.local_variable);

                    local_var_space += 8;
                    offset += 8;
                    idents.node_set_value(ident, offset);
                }
            }

            if(codegen.peek_type() == tokenizer.comma) {
                codegen.discard();
            } else {}
            continue; // @BUG: I need this for some reason.
        }
    } else {}

    codegen.function_prologue(num_args, local_var_space, offset);
}

fn clear_local_vars_in(node) [i, next] {
    i = 0;

    switch (idents.node_get_type(node)) {
        endcase;
    case itypes.local_variable:
    case itypes.local_buffer:
        idents.node_set_type(node, itypes.none);
        endcase;
    }

    node = idents.node_addr(node);

    loop {
        if(i == idents.max_nexts) {
            return;
        } else {}

        next = idents.trie_node_next(node, i);

        i += 1;

        if(next) {
            clear_local_vars_in(next);
            continue;
        } else {
            continue;
        }
    }
}

fn clear_local_vars() [node] {
    node = codegen.current_file_root[0];
    clear_local_vars_in(node);
}

fn parse_function() [fn_ident, root, fn_addr, fn_attrs] {
    codegen.discard();

    codegen.return_was_used[0] = 0;

    fn_ident = codegen.expect_token(tokenizer.identifier, "Expected identifier after `fn`");

    fn_addr = writer.code_addr();
    idents.node_set_type(fn_ident, itypes.function_addr);
    idents.node_set_value(fn_ident, fn_addr);
    idents.node_set_attribute(fn_ident, 1); // We assume it returns for recursion purposes

    codegen.add_symbol(fn_ident);
    codegen.discard();

    read_stack_frame();

    root = codegen.current_file_root[0];
    // Can be exited by falling out of block
    fn_attrs = codegen.parse_block(root);

    codegen.expect_token(tokenizer.closing_curly_brace, "Expected `}` after function body");
    codegen.discard();

    // TODO: Only do this if the block end can be reached
    codegen.eval_type[0] = itypes.runtime_int;
    if(fn_attrs) {
        codegen.return_evaluated();
    } else { }

    // Can also be exited by `return` statement
    fn_attrs |= codegen.return_was_used[0];

    idents.node_set_attribute(fn_ident, fn_attrs);

    clear_local_vars();
}

fn parse_zeroes() [ident, tmp] {
    codegen.discard();

    ident = codegen.expect_token(tokenizer.identifier, "Expected identifier after `zeroes`");

    tmp = writer.section_addr(writer.bss);
    idents.node_set_value(ident, tmp);

    codegen.add_symbol(ident);
    codegen.discard();

    if(codegen.peek_type() == tokenizer.open_square_bracket) {
        idents.node_set_type(ident, itypes.global_buffer);

        codegen.discard();

        tmp = codegen.eval_comptime_expr();

        codegen.expect_token(tokenizer.closing_square_bracket, "Expected `]` after zeroes decl size");
        codegen.discard();

        writer.write_generic(0, writer.bss, tmp);

        idents.node_set_attribute(ident, tmp);
    } else {
        idents.node_set_type(ident, itypes.global_variable);
        writer.write_generic(0, writer.bss, 8);
    }

    codegen.expect_token(tokenizer.semicolon, "Expected `;` after zeroes declaration");
    codegen.discard();
}

fn parse_top_level() {
    codegen.current_file_root[0] = idents.alloc();
    if(root_root[0]) {

    } else {
        root_root[0] = codegen.current_file_root[0];
    }
    add_builtins_to_current_file();

    loop {
        switch(codegen.peek_type()) {
            @todo("parse_top_level default");

        case tokenizer.end_of_file:
            codegen.discard();
            return;

        case tokenizer.keyword_zeroes:
            parse_zeroes();
            continue;

        case tokenizer.keyword_fn:
            parse_function();
            continue;

        case tokenizer.keyword_enum:
            parse_enum_decl();
            continue;

        case tokenizer.keyword_comptime:
            parse_comptime_decl();
            continue;

        case tokenizer.keyword_import:
            parse_import();
            continue;
        }
    }
}

fn init(base_addr) {
    idents.init();
    writer.init(base_addr);
    import_file_root[0] = idents.alloc();
    parse_top_level_ptr[0] = parse_top_level;

    builtins.create_builtin_node();
}
