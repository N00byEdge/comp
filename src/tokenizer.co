import "src/identifiers.co" identifiers;
import "src/printer.co" printer;
import "src/source.co" source;
import "src/strings.co" strings;
import "src/writer.co" writer;

fn error(fail_message) [tmp] {
    writer.dump();
    printer.print_string(fail_message);
    printer.putchar('\n');
    printer.print_string("while parsing ");
    printer.print_string(source.file_name);
    printer.putchar(':');
    tmp = source.current_line[0];
    printer.print_decimal(tmp);
    printer.putchar('\n');
    printer.exit(1);
    unreachable;
}

zeroes token_type[8];
zeroes token_value[8];
zeroes buffer[0x1000]; // Buffer for string literals, identifers etc
zeroes token_line[8];

enum {
    none,

    end_of_file,

    // {}
    open_curly_brace,
    closing_curly_brace,

    // ()
    open_paren,
    closing_paren,

    // []
    open_square_bracket,
    closing_square_bracket,

    less_than, // <
    less_than_equal, // <=

    greater_than, // >
    greater_than_equal, // >=

    shift_left, // <<
    shift_left_inplace, // <<=

    shift_right, // >>
    shift_right_inplace, // >>=

    assignment, // =
    equals, // ==
    not_equals, // !=

    comma, // ,
    colon, // :
    semicolon, // ;

    question_mark, // ?

    bitnot, // ~

    dot, // .

    dot_dot_dot, // ...

    bitor, // |
    bitor_inplace, // |=

    bitxor, // ^
    bitxor_inplace, // ^=

    bitand, // &
    bitand_inplace, // &=

    division, // /
    division_inplace, // /=

    modulus, // %
    modulus_inplace, // %=

    multiplication, // *
    multiplication_inplace, // *=

    addition, // +
    addition_inplace, // +=

    subtraction, // -
    subtraction_inplace, // -=

    // like `420` or `0x69`, value is the value of the literal
    int_literal,

    // Like `"Hello, \x00 is a valid byte"`
    // Value is the length of the string
    string_literal,

    // like `my_variable`, value is pointer to node
    identifier,

    // All the keywords in the language
    keyword_break,
    keyword_case,
    keyword_comptime,
    keyword_continue,
    keyword_else,
    keyword_endcase,
    keyword_enum,
    keyword_fn,
    keyword_if,
    keyword_import,
    keyword_loop,
    keyword_return,
    keyword_switch,
    keyword_undefined,
    keyword_unreachable,
    keyword_zeroes,
};

fn is_first_ident_chr(idx) {
    switch(source.peek(idx)) {
        return 0;

    case 'a'...'z':
    case 'A'...'Z':
    case '@':
    case '_':
        return 1;
    }
    unreachable;
}

fn is_ident_chr(idx) {
    switch(source.peek(idx)) {
        return 0;
        
    case 'a'...'z':
    case 'A'...'Z':
    case '0'...'9':
    case '@':
    case '_':
        return 1;
    }
    unreachable;
}

fn parse_identifier(node) [ptr] {
    @assert(is_first_ident_chr(0));

    @write64(token_type, identifier);
    ptr = buffer;

    loop {
        if(is_ident_chr(0)) {
            ptr[0] = source.consume(1);
            node = identifiers.lookup(ptr, node);
            ptr += 1;
            continue;
        } else {
            @write8(ptr, 0);
            @write64(token_value, node);
            return;
        }
    }
}

fn attempt_keyword(keyword_text, keyword, node) [idx, chr] {
    idx = 0;
    loop {
        chr = @read8(keyword_text);
        if(chr) {
            if(source.peek(idx) == chr) {
                keyword_text += 1;
                idx += 1;
                continue;
            } else {
                parse_identifier(node);
                return;
            }
        } else {
            if(is_ident_chr(idx)) {
                parse_identifier(node);
                return;
            } else {
                source.consume(idx);
                @write64(token_type, keyword);
                return;
            }
        }
    }
}

fn read_int_literal_token_hex() [chr] {
    @write64(token_type, int_literal);
    @write64(token_value, 0);

    loop {
        chr = source.peek(0);
        switch(chr) {
            return;

        case '0'...'9':
        case 'a'...'f':
        case 'A'...'F':
            token_value[0] <<= 4;
            token_value[0] += strings.chr_val(chr);
            source.consume(1);
            continue;
        }
    }
}

fn read_int_literal_token_decimal() [chr] {
    @write64(token_type, int_literal);
    @write64(token_value, 0);

    loop {
        chr = source.peek(0);
        switch(chr) {
            return;
        case '0'...'9':
            token_value[0] *= 10;
            token_value[0] += strings.chr_val(chr);
            source.consume(1);
            continue;
        }
    }
}

fn read_int_literal_token_zero() {
    // peek(0) == '0' when we enter here
    if(source.peek(1) == 'x') {
        source.consume(2);
        return read_int_literal_token_hex();
    } else {
        return read_int_literal_token_decimal();
    }
}

fn peek_val(peek) {
    peek = source.peek(peek);
    return strings.chr_val(peek);
}

fn read_char_literal_token() [chr] {
    @write64(token_type, int_literal);
    chr = source.peek(0);

    if(chr == '\\') {
        switch(source.peek(1)) {
            error("Bad char literal escaped char");

        case 'n':
            source.consume(3);
            return '\n';

        case 'r':
            source.consume(3);
            return '\r';

        case 't':
            source.consume(3);
            return '\t';

        case '\\':
            source.consume(3);
            return '\\';

        case '\'':
            source.consume(3);
            return '\'';
            
        case 'x':
            chr = peek_val(2);
            chr <<= 4;
            chr |= peek_val(3);
            source.consume(5);
            return chr;
        }
        @todo("read_char_literal_token escape");
    } else {
        @write64(token_value, chr);
        source.consume(2);
        return;
    }
}

fn read_string_literal_token() [len] {
    @write64(token_type, string_literal);
    len = 0;

    loop {
        switch(source.peek(0)) {
            buffer[len] = source.consume(1);
            len += 1;
            continue;
        case '"':
            source.consume(1);
            buffer[len] = 0;
            @write64(token_value, len);
            return;
        case '\\':
            switch(source.peek(1)) {
                error("Bad string literal escaped char");

            case 'n':
                buffer[len] = '\n';
                source.consume(2);
                len += 1;
                continue;

            case 'r':
                buffer[len] = '\r';
                source.consume(2);
                len += 1;
                continue;

            case 't':
                buffer[len] = '\t';
                source.consume(2);
                len += 1;
                continue;

            case '\\':
                buffer[len] = '\\';
                source.consume(2);
                len += 1;
                continue;

            case '\'':
                buffer[len] = '\'';
                source.consume(2);
                len += 1;
                continue;

            case 'x':
                buffer[len] = peek_val(2);
                buffer[len] <<= 4;
                buffer[len] |= peek_val(3);
                source.consume(4);
                len += 1;
                continue;
            }
        }
    }
}

fn fill_token(node) [chr] {
    if(token_type[0] == none) {
        loop {
            token_line[0] = source.current_line[0];

            switch(source.peek(0)) {
                printer.print_string("Unexpected character in fill_token: '");
                chr = source.peek(0);
                printer.putchar(chr);
                printer.print_string("' (0x");
                printer.print_hex(chr);
                printer.print_string(")\n");
                printer.exit(1);

            // EOF
            case 0:
                token_type[0] = end_of_file;
                return;

            // No one cares about whitespace
            case '\n':
            case '\t':
            case ' ':
                source.consume(1);
                continue;

            case '0':
                read_int_literal_token_zero();
                return;

            case '1'...'9':
                read_int_literal_token_decimal();
                return;

            case '"': source.consume(1);
                read_string_literal_token();
                return;

            case '\'': source.consume(1);
                token_value[0] = read_char_literal_token();
                return;

            case '=':
                switch(source.peek(1)) {
                    source.consume(1);
                    token_type[0] = assignment;
                    return;
                case '=':
                    source.consume(2);
                    token_type[0] = equals;
                    return;
                }

            case '!':
                switch(source.peek(1)) {
                    @todo("fill_token (!)");
                case '=':
                    source.consume(2);
                    token_type[0] = not_equals;
                    return;
                }

            case '?':
                source.consume(1);
                token_type[0] = question_mark;
                return;

            case ',':
                source.consume(1);
                token_type[0] = comma;
                return;

            case ':':
                source.consume(1);
                token_type[0] = colon;
                return;

            case ';':
                source.consume(1);
                token_type[0] = semicolon;
                return;

            case '.':
                if((source.peek(1) == '.') & (source.peek(2) == '.')) {
                    source.consume(3);
                    token_type[0] = dot_dot_dot;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = dot;
                    return;
                }

            case '{':
                source.consume(1);
                token_type[0] = open_curly_brace;
                return;

            case '}':
                source.consume(1);
                token_type[0] = closing_curly_brace;
                return;

            case '(':
                source.consume(1);
                token_type[0] = open_paren;
                return;

            case ')':
                source.consume(1);
                token_type[0] = closing_paren;
                return;

            case '[':
                source.consume(1);
                token_type[0] = open_square_bracket;
                return;

            case ']':
                source.consume(1);
                token_type[0] = closing_square_bracket;
                return;

            case '~':
                source.consume(1);
                token_type[0] = bitnot;
                return;

            case '|':
                if(source.peek(1) == '=') {
                    source.consume(2);
                    token_type[0] = bitor_inplace;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = bitor;
                    return;
                }

            case '&':
                if(source.peek(1) == '=') {
                    source.consume(2);
                    token_type[0] = bitand_inplace;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = bitand;
                    return;
                }

            case '^':
                if(source.peek(1) == '=') {
                    source.consume(2);
                    token_type[0] = bitxor_inplace;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = bitxor;
                    return;
                }

            case '%':
                if(source.peek(1) == '=') {
                    source.consume(2);
                    token_type[0] = modulus_inplace;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = modulus;
                    return;
                }

            case '*':
                if(source.peek(1) == '=') {
                    source.consume(2);
                    token_type[0] = multiplication_inplace;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = multiplication;
                    return;
                }

            case '+':
                if(source.peek(1) == '=') {
                    source.consume(2);
                    token_type[0] = addition_inplace;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = addition;
                    return;
                }

            case '-':
                if(source.peek(1) == '=') {
                    source.consume(2);
                    token_type[0] = subtraction_inplace;
                    return;
                } else {
                    source.consume(1);
                    token_type[0] = subtraction;
                    return;
                }

            case '/':
                switch(source.peek(1)) {
                    source.consume(1);
                    token_type[0] = division;
                    return;

                case '=': source.consume(2);
                    source.consume(2);
                    token_type[0] = division_inplace;
                    return;

                case '/':
                    source.consume(2);
                    // Line comment like this one
                    loop {
                        if(source.consume(1) != '\n') {
                            continue;
                        } else {
                            break;
                        }
                    }
                    continue;
                }

            case '<':
                switch(source.peek(1)) {
                    source.consume(1);
                    token_type[0] = less_than;
                    return;

                case '<':
                    if(source.peek(2) == '=') {
                        source.consume(3);
                        token_type[0] = shift_left_inplace;
                        return;
                    } else {
                        source.consume(2);
                        token_type[0] = shift_left;
                        return;
                    }

                case '=':
                    source.consume(2);
                    token_type[0] = less_than_equal;
                    return;
                }

            case '>':
                switch(source.peek(1)) {
                    source.consume(1);
                    token_type[0] = greater_than;
                    return;

                case '>':
                    if(source.peek(2) == '=') {
                        source.consume(3);
                        token_type[0] = shift_right_inplace;
                        return;
                    } else {
                        source.consume(2);
                        token_type[0] = shift_right;
                        return;
                    }

                case '=':
                    source.consume(2);
                    token_type[0] = greater_than_equal;
                    return;
                }

            case 'a'...'z':
            case 'A'...'Z':
            case '@':
            case '_':
                return parse_identifier(node);

            case 'b':
                return attempt_keyword("break", keyword_break, node);

            case 'c':
                switch(source.peek(1)) {
                    return parse_identifier(node);
                case 'a':
                    return attempt_keyword("case", keyword_case, node);
                case 'o':
                    switch(source.peek(2)) {
                        return parse_identifier(node);
                    case 'm':
                        return attempt_keyword("comptime", keyword_comptime, node);
                    case 'n':
                        return attempt_keyword("continue", keyword_continue, node);
                    }
                }

            case 'e':
                if(source.peek(1) == 'n') {
                    switch(source.peek(2)) {
                        return parse_identifier(node);
                    case 'd':
                        return attempt_keyword("endcase", keyword_endcase, node);
                    case 'u':
                        return attempt_keyword("enum", keyword_enum, node);
                    }
                } else {
                    return attempt_keyword("else", keyword_else, node);
                }

            case 'f':
                return attempt_keyword("fn", keyword_fn, node);

            case 'i':
                switch(source.peek(1)) {
                    return parse_identifier(node);
                case 'f':
                    return attempt_keyword("if", keyword_if, node);
                case 'm':
                    return attempt_keyword("import", keyword_import, node);
                }

            case 'l':
                return attempt_keyword("loop", keyword_loop, node);

            case 'r':
                return attempt_keyword("return", keyword_return, node);

            case 's':
                return attempt_keyword("switch", keyword_switch, node);

            case 'u':
                if(source.peek(1) == 'n') {
                    switch(source.peek(2)) {
                        return parse_identifier(node);
                    case 'd':
                        return attempt_keyword("undefined", keyword_undefined, node);
                    case 'r':
                        return attempt_keyword("unreachable", keyword_unreachable, node);
                    }
                } else {
                    return parse_identifier(node);
                }

            case 'z':
                return attempt_keyword("zeroes", keyword_zeroes, node);
            }
        }
    } else { }
}

fn peek_type(node) {
    fill_token(node);
    return token_type[0];
}

fn expect_token(expected_type, fail_message, node) {
    fill_token(node);
    if(expected_type == token_type[0]) {
        return token_value[0];
    } else {
        printer.print_string("Unexpected token type: ");
        error(fail_message);
        unreachable;
    }
}

fn peek_value(node) {
    fill_token(node);
    return token_value[0];
}

fn discard(node) {
    fill_token(node);
    token_type[0] = none;
}
